; Implementation of _complete_ term rewriting system in Egison

(load-file "term.egi")

(define $equation
  (matcher
    {[<equation $ $> [term term]
      {[<Equation $l $r> {[l r]}] ; rewrite should be oriented left-to-right
       [_ {}]}]
     [$ something
      {[$tgt {tgt}]}]
     }))

(define $eql (lambda [$x $y] <Equation x y>))

(define $efv
  (lambda [$eq]
    (unique
      (match eq equation
        {[<equation $l $r> (append (fv l) (fv r))]}))))

(define $esubst
  (match-lambda [something equation]
    {[[$u <equation $l $r>] <Equation (tsubst u l) (tsubst u r)>]}))

; term -> {equation} -> term
(define $normalize
  (match-lambda [term (multiset equation)]
    {[[<subterm $subtm $context>
       (& <cons <equation <unify-ord ,subtm $sigma> $r> _> $axioms)]
      (normalize (debug/fn show-term (context (tsubst sigma r))) axioms)]
     [[$t _] t]
     }))

(assert-equal "normalize1"
  (normalize (app "+" two one) axioms-of-nat)
  three)
(assert-equal "normalize2"
  (normalize (p (p y y') e) axioms-of-groups)
  e)
(assert-equal "normalize3"
  (normalize (p (p y z) (i (p y z))) axioms-of-groups)
  e)
(assert-equal "normalize 4"
  (normalize (p e x) axioms-of-groups)
  (p e x))

;;======================= Knuth-Bendix completion ==============================

; rename free variables of each axioms so that they don't collide
(define $rename-problem
  (match-lambda [integer (list equation)]
    {[[$n <nil>]
      {}]
     [[$n <cons $e $es>]
      (let {[$u (zip (efv e)
                     ; n, n+1, ... の新しいvarを生成
                     (map (lambda $x <Var x>) (from n)))]}
        (cons (esubst u e)
              (rename-problem (+ n (length u)) es)))]}))

(define $rename-problem2
  (match-lambda [integer (list equation) (multiset equation)]
    {[[$n <nil> $es']
      [{} es']]
     [[$n <cons $e $es> <cons ,e $es'>]
      (let {[$u (zip (efv e)
                     ; n, n+1, ... の新しいvarを生成
                     (map (lambda $x <Var x>) (from n)))]}
        (let {[$r (rename-problem2 (+ n (length u)) es es')]}
          [(cons (esubst u e) (fst r)) (cons (esubst u e) (snd r))]))]
     [[$n <cons $e $es> $es']
      (let {[$u (zip (efv e)
                     ; n, n+1, ... の新しいvarを生成
                     (map (lambda $x <Var x>) (from n)))]}
        (let {[$r (rename-problem2 (+ n (length u)) es es')]}
          [(cons (esubst u e) (fst r)) (snd r)]))]
     }))

(define $unique-unordered-pairs
  (lambda [$xs]
    (letrec {[$loop-fn
              (lambda [$xs $ys]
                (match [xs ys] [(list equation) (multiset equation)]
                  {[[<nil> _] ys]
                   [[<cons $x $rs> <cons ,x _>] (loop-fn rs ys)]
                   [[<cons <equation $l $r> $rs> <cons <equation ,r ,l> _>]
                      (loop-fn rs ys)]
                   [[<cons $x $rs>  _] (loop-fn rs {@ys x})]}))]}
      (loop-fn xs {}))))

(define $confluent-pair?
  (match-lambda [something equation]
    {[[$axioms <equation $s $t>]
        (eq? (normalize (debug/fn show-term s) axioms)
             (normalize (debug/fn show-term t) axioms))
      ]}))

; pairs (s, t) are kept in the form of <Equation s t>
(define $critical-pairs
  (lambda [$axioms $new-axioms]
    (unique-unordered-pairs
      (match-all (rename-problem2 0 axioms new-axioms)
                 [(multiset equation) (multiset equation)]
        {[[<cons (& $eq1 <equation $l2 $r2>) _>
           <cons (& !,eq1
                    <equation
                      <subterm (& !<var _> <unify ,l2 $sigma>) $context>
                      $r1>) _>]
          (eql (tsubst sigma r1) (tsubst sigma (context r2)))]
         [[<cons (& $eq1
                    <equation
                      <subterm (& !<var _> (later <unify ,l2 $sigma>)) $context>
                      $r1>) _>
           <cons (& !,eq1
                    <equation $l2 $r2>) _>]
          (eql (tsubst sigma r1) (tsubst sigma (context r2)))]
         }))))

(define $orient-equation
  (match-lambda equation
    {[<equation $l $r>
                (if (succ? l r) (eql l r) (eql r l))]}))

(define $find-new-axioms
  (lambda [$axioms $new-axioms]
    (map orient-equation
         (filter (lambda [$x] (not (confluent-pair? axioms x)))
                 (critical-pairs axioms new-axioms)))))

; new-axioms is a subset of axioms
; TODO: orient critical pairs in right directions so rewriting doesn't explode
(define $complete-loop
  (lambda [$axioms $new-axioms]
    (match (debug/fn show-equations (find-new-axioms axioms new-axioms)) (multiset something)
      {[<nil> axioms]
       [$c (complete-loop (union axioms c) c)]
       })))

(define $complete
  (complete-loop $1 $1))

(define $crit1 (critical-pairs axioms-of-groups axioms-of-groups))
; {y * i(y) * x = e * x}
(define $c1 (find-new-axioms axioms-of-groups axioms-of-groups))
(define $d1 (union axioms-of-groups c1))
; {e * i(i(x5)) = x5 * e}
(define $crit2 (critical-pairs d1 c1))
(define $c2 (find-new-axioms d1 c1))
(define $d2 (union d1 c2))
(define $c3 (find-new-axioms d2 c2))

(show-equations (critical-pairs d2 c2))
(define $s2
  <Compound "*" {<Compound "e" {}> <Compound "i" {<Compound "i" {<Var 5>}>}>}>)
(define $t2
  <Compound "*" {<Var 5> <Compound "e" {}>}>)

;;==============================================================================

(define $show-equation
  (match-lambda equation
    {[<equation $x $y> (S.concat {(show-term x) " = " (show-term y)})]}))

(define $show-equations
  (col-show/fn show-equation $1))

;;==============================================================================

(define $p (app "*" $1 $2)) ; product
(define $i (app "i" $1))    ; inverse
(define $e (app "e"))       ; 単位元

(define $a' (i a))
(define $b' (i b))
(define $c' (i c))
(define $d' (i d))

(define $x' (i x))
(define $y' (i y))
(define $z' (i z))
(define $w' (i w))

(define $axioms-of-groups
  {(eql (p (p x y) z) (p x (p y z)))
   (eql (p x x') e)            ; x * x' = e
   (eql (p x e) x)             ; x * e = x
  })

(define $axioms-of-groups-complete
  {@axioms-of-groups
   (eql (i (p y x)) (p x' y')) ; (y * x)' = x' * y'
   (eql (p y' (p y x)) x)      ; y' * (y * x) = x
   (eql (p x' x) e)            ; x' * x = e
   (eql (i e) e)               ; e' = e
   (eql (i x') x)              ; x'' = x
   (eql (p e x) x)             ; e * x = x
   (eql (p x (p x' y)) y)      ; x * (x' * y) = y
  })

; (a * a')' = e
(define $g1' (i (p a a')))
(define $g1 (eql g1' e))

; (((b * c) * d)' * b)' * d' = c
(define $g2' (p (i (p (i (p (p b c) d)) b))
                (i d)))
(define $g2 (eql g2' c))
; (((b * c) * d)' * b)' * d'
; = (d' * (b * c)' * b)' * d'
; = (d' * c' * b' * b)' * d'
; = (d' * c' * e)' * d'
; = (d' * c')' * d'
; = c'' * d'' * d'
; = c'' * d * d'
; = c'' * e
; = c * e
; = c

(define $g3 (eql (p g1' g2') c))

; e * a = a
(define $g4 (eql (p e a) a))

; a' * a = e
(define $g5 (eql (p a' a) e))

;;==============================================================================

(define $zero  (app "O"))
(define $one   (app "S" (app "O")))
(define $two   (app "S" (app "S" (app "O"))))
(define $three (app "S" (app "S" (app "S" (app "O")))))
(define $four  (app "S" (app "S" (app "S" (app "S" (app "O"))))))

(define $axioms-of-nat
   ; O + x = x
  {(eql (app "+" zero x) x)
   ; S(m) + n = m + S(n)
   (eql (app "+" (app "S" x) y) (app "+" x (app "S" y)))
   })

(define $p7 (eql (app "+" one two) three))
(define $p8 (eql (app "+" zero two) two))

(assert-equal "normalize"
  (normalize (app "+" one two) axioms-of-nat)
  three)
