(define $unordered-pair
 (lambda [$m]
  (matcher
    {[<pair $ $> [m m] {[[$x $y] {[x y] [y x]}]}]
     [$ [something] {[$tgt {tgt}]}]})))

(define $unique
  (match-all-lambda (list eq)
    [<join _ <cons $z !<join _ <cons ,z _>>>> z]))

(define $compare-c/fn
  (lambda [$ord $c1 $c2]
    (match [c1 c2] [(list something) (list something)]
      {[[<nil> <nil>] <Equal>]
       [[<nil> _] <Less>]
       [[_ <nil>] <Greater>]
       [[<cons $x $xs> <cons ,x $ys>] (compare-c/fn ord xs ys)]
       [[<cons $x _> <cons $y _>] (ord x y)]})))

(define $neq? (lambda [$x $y] (not (eq? x y))))

; ============================== pretty printing ===============================

(define $append-strings
  (lambda [$strs]
    (foldl (lambda [$acc $x] (append-string acc x)) "" strs)))

(define $concat-strings
  (lambda [$strs $sep]
    (append-strings
      (match-all strs (list string)
        {[<nil> ""]
         [<join _ <cons $x !<nil>>> (append-string x sep)]
         [<join _ <cons $x <nil>>> x]
         }))))
